         TITLE 'P12DSH3.MLC - Calc DFP 128 Bit Standard Deviation'
*********************************************************************
* Program ID.  P12DSH3.MLC
* Author.      Don Higgins.
* Date.        06/20/07.
*********************************************************************
* 02/20/08 DSH3 CALC STANDARD DEVIATION USING ONLY DFP INSTRUCTIONS
* 02/27/08 DSH3 USE Z390 MAC\SQXTR.MAC AND LINKLIB\SQXTR.OBJ SUPPORT   
*********************************************************************
P10DSH3  ZMFACC CODE,START,NAME='DON HIGGINS'
         COPY  ASMMSP      STRUCTURED PROGRAMMING MACRO LIBRARY
         CALL  STDDEVLD,(LDX,N,LDSD)  CALCULATE STANDARD DEVIATION
*
* DISPLAY STD DEVIATION IN SCIENTIFIC NOTATION FOR DEBUGGIN
*
         CTD   CTD_LD,IN=LDSD,OUT=DLDSD
         ZMFACC CODE,END
N        DC    F'4'        INTEGER NUMBER OF ARRAY ELEMENTS
         DS     0D
         ZMFACC INPUT,START
LDX      DC    LD'1,2,3,6' DECIMAL EXTENDED PRECISION (DFP) ARRAY 
         ZMFACC INPUT,END
         ZMFACC OUTPUT,START
LDSD     DC    LD'0'       STANDARD DEVIATION DFP VALUE
DLDSD    DC    CL45' '     STANDARD DEVIATION IN SCIENTIFIC NOTATION 
         ZMFACC OUTPUT,END
         TITLE 'STDDEVLD - CALC DFP 128 BIT STANDARD DEVIATION'
STDDEVLD CSECT
         USING *,15
         SAVE  (14,12)
         BALR  12,0
         USING *,12
*
* CALCULATE DFP 128 BIT STANDARD DEVIATION FOR ARRAY OF DFP ELEMENTS
*
* CALL PARAMETERS:
*
*  1 = INPUT  ADDRESS OF DFP ARRAY ELEMENTS (16 BYTE DFP LD FORMAT)
*  2 - INPUT  FULL WORD ARRAY ELEMENT COUNT (MUST BE >= 1)
*  3 - OUTPUT STANDARD DEVIATION IN 16 BYTE DFP LD FORMAT
*
* RETURN CODES:
*
*   0 - STANDARD DEVIATION STORED SUCCESSFULLY IN PARAMETER 3
*  16 - ELEMENT COUNT LESS THAN 1 (STD.DEV. UNDEFINED)
*
*
* CALCULATE DFP LDXM MEAN = (SUM X(I))/N AND SAVE IN F4+F6
*
         LM    R3,R5,0(R1) R3=A(ARRAY) R4=A(N) R5=A(LDSD)
         LGF   R4,0(R4)    R4=N ELEMENT COUNT
         LR    R6,R3       R6=AARRY ELEMENT ADDRESS
         LR    R7,R4       R7=ELEMENTS TO PROCESS IN LOOP         
         IF    (CHI,R7,LT,1)
               RETURN (14,12),RC=16  EXIT WITH RC=16 IF COUNT < 1
         ENDIF
         SXTR  F4,F4,F4    F4+F6 = SUM X(I) TO CALC MEAN
         DO    WHILE=(CHI,R7,GT,0)
               LD  F1,0(R6) F1+F3 = ELEMENT
               LD  F3,8(R6)
               AXTR F4,F4,F1  ADD ELEMENT TO SUM
               AHI  R6,16   NEXT ELEMENT
               AHI  R7,-1   REDUCE ELEMENTS REMAINING
         ENDDO
         CXGTR F1,R4       F1+F3 = N
         DXTR  F4,F4,F1    F4+F6 = F4+F6 / F1+F3 = DFP LD MEAN
*
* CALCULATE SUM OF VARIANCE SQUARED IN F0+F2
*       
         LR    R6,R3       R6 = ADDRESS FIRST ARRAY ELEMENT
         LR    R7,R4       R7 = ELEMENTS TO PROCESS IN LOOP
         SXTR  F0,F0,F0    F0+F2 = (SUM X(I)-MEAN)**2
         DO    WHILE=(CHI,R7,GT,0)
               LD  F1,0(R6) F1+F3 = DFP LD ELEMENT
               LD  F3,8(R6)
               SXTR F1,F1,F4  SUBTRACT MEAN
               MXTR F1,F1,F1  SQUARE   DIFF
               AXTR F0,F0,F1  ADD      DIFF * DIFF
               AHI R6,16      NEXT ELEMENT
               AHI R7,-1      REDUCE ELEMENTS REMAINING
         ENDDO
         CXGTR F1,R4          F1+F3 = N
         DXTR  F0,F0,F1       F0+F2 = F0+F2 / F1+F3 = (SUM DIFF*DIFF)/N
*
* CALCULATE STANDARD DEVIATION (USES SQXTR.MAC AND SQXTR.MLC)
*
         SQXTR F0,F0     CALC SQRT SUM OF VAR**2 IN F0+F2
         STD   F0,0(R5)  STORE F0+F2 LD STANDARD DEVIATION RESULT
         STD   F2,8(R5)
         RETURN (14,12),RC=0
LDXM     DS    LD        VARIANCE FOR DEBUG DISPLAY
LDXV2    DS    LD        (SUM DIFF*DIFF)/N FOR DEBUG DISPLAY
         END
