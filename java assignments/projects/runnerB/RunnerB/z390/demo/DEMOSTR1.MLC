*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
*********************************************************************
* 05/15/06 RPI 316 DEMO USE OF STRUCTURED PROGRAMMING MACROS
*********************************************************************
         PRINT NOGEN
         COPY  ASMMSP      COPY STRUCTURED MACROS FROM STRMAC\*.*
DEMOSTR1 SUBENTRY
         WTO   'DEMOSTR1 - DEMO STRUCTURED PROGAMMING MACROS'
         ZAP   PCOUNT,=P'1'
         DO    WHILE=(CP,PCOUNT,LT,=P'3')
               WTO '  WHILE LOOP TWO TIMES'
               AP    PCOUNT,=P'1'
         ENDDO
         ZAP   PCOUNT,=P'0'
         DO    UNTIL=(CP,PCOUNT,EQ,=P'3')
               AP    PCOUNT,=P'1'
               WTO '  UNTIL LOOP THREE TIMES'
         ENDDO
         IF    CP,PCOUNT,EQ,=P'3'
               WTO '  IF PCOUNT = 3'
         ENDIF
         IF    (CP,PCOUNT,EQ,=P'2')
               WTO '  IF PCOUNT = 2'
         ELSE
               WTO '  IF PCOUNT NE 2'
         ENDIF
         IF    (CP,PCOUNT,EQ,=P'2')
               WTO '  IF PCOUNT = 2'
         ELSEIF (CP,PCOUNT,EQ,=P'3')
               WTO '  ELSE IF PCOUNT EQ 3'
         ENDIF
         WTO   'DEMOSTR1 ENDING'
         SUBEXIT
         LTORG
PCOUNT   DC    P'0'
         END
